buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.dokka' version '0.10.0'
    id 'com.palantir.git-version' version '0.12.2'
    id "co.riiid.gradle" version "0.4.2"

    id 'org.jetbrains.kotlin.jvm' version '1.3.70' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.3.70' apply false
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.70' apply false
    id 'org.jetbrains.kotlin.plugin.noarg' version '1.3.70' apply false
}

def gversion = '1.2'//-' + gitVersion()
allprojects {
    repositories {
        mavenCentral()
        maven { url "https://nexus.bedatadriven.com/content/groups/public" }
        maven { url "https://jitpack.io/" }
    }

    group 'uk.ac.cam.jm2186'
    version gversion
}

println version

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.kapt'
    apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
    apply plugin: 'org.jetbrains.kotlin.plugin.noarg'

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
        testFixturesImplementation "org.junit.jupiter:junit-jupiter:5.5.2"

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        testFixturesImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        implementation group: 'log4j', name: 'log4j', version: '1.2.17'
        //implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.1'
        implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'

        kapt 'org.hibernate:hibernate-jpamodelgen:5.4.9.Final'
        kaptTest 'org.hibernate:hibernate-jpamodelgen:5.4.9.Final'
    }

    noArg {
        annotation("javax.persistence.Entity")
        annotation("javax.persistence.Embeddable")
        annotation("javax.persistence.MappedSuperclass")
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    configuration {
        jdkVersion = 8
    }
}


github {
    owner = 'jjurm'
    repo = 'graffs'
    String gprkey = project.findProperty("gpr.key")
    if (gprkey != null && !gprkey.isEmpty()) {
        token = gprkey
    }
    tagName = version
    targetCommitish = 'master'
    name = "v${version}"
    //body = """"""
    assets = [
            "${project(':cli').buildDir}/libs/graffs-${version}.jar"
    ]
}


githubRelease.dependsOn("build")
githubRelease.dependsOn(":cli:shadowJar")
task build {}
build.dependsOn project(':cli').tasks.build
