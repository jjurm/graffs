plugins {
    id 'application'
    id 'idea'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
}

mainClassName = "uk.ac.cam.jm2186.graffs.cli.GraffsKt"
archivesBaseName = rootProject.name

defaultTasks 'run'

buildConfig {
    packageName = "uk.ac.cam.jm2186.graffs"
    appName = "graffs-cli"
    buildConfigField 'java.time.Instant', 'BUILD_DATE', 'java.time.Instant.ofEpochMilli(' + System.currentTimeMillis() + 'L)'
}
// Java version for the BuildConfig generated class
def javaVersion = JavaVersion.VERSION_1_8
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

idea {
    module {
        def buildConfigGenerated = file('build/gen/buildconfig/src/main')
        sourceDirs += buildConfigGenerated
        generatedSourceDirs += buildConfigGenerated
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    archiveClassifier.set('')
    minimize {
        exclude(dependency('org.hibernate:hibernate-core:.*'))
        exclude(dependency('org.hibernate:hibernate-c3p0:.*'))
        exclude(dependency('com.h2database:h2:.*'))
        exclude(dependency('log4j:log4j:.*'))
        exclude(dependency('commons-logging:commons-logging:.*'))
        exclude(dependency('org.renjin:.*:.*'))
    }
    doLast {
        copy {
            from shadowJar
            into 'build/libs'
            rename { String filename -> "${archivesBaseName}-last.jar" }
        }
    }
}

def serverName = "rio"
def jarDir = "~/scratch/jars"

task jarUpload(dependsOn: shadowJar) {
    doLast {
        exec {
            commandLine "scp", "${buildDir}/libs/${archivesBaseName}-${version}.jar", "${serverName}:${jarDir}/${archivesBaseName}-${version}.jar"
        }
        exec {
            commandLine "ssh", serverName, "echo 'java -jar ${jarDir}/${archivesBaseName}-${version}.jar \$@' > ~/scratch/${archivesBaseName} ; chmod u+x ~/scratch/${archivesBaseName} ; _GRAFFS_COMPLETE=bash graffs > ./graffs-completion.sh"
        }
    }
}

dependencies {
    api project(':api')
    api project(':core')
    api project(':graph')
    api project(':robustness')

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.20'

    implementation group: 'com.github.ajalt', name: 'clikt', version: '2.5.0'
    implementation group: 'org.hibernate', name: 'hibernate-core', version: '5.4.11.Final'
    implementation group: 'org.hibernate', name: 'hibernate-c3p0', version: '5.4.11.Final'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.200'

    def noSlf4j = { exclude group: 'org.slf4j', module: 'slf4j-api' }
    implementation 'tech.tablesaw:tablesaw-core:0.37.2', noSlf4j
    implementation 'tech.tablesaw:tablesaw-jsplot:0.37.2', noSlf4j
    implementation 'tech.tablesaw:tablesaw-html:0.37.2', noSlf4j
    implementation 'com.github.Simonwep:java-express:0.1.1'

    implementation group: 'org.graphstream', name: 'gs-algo', version: '1.3'
}
